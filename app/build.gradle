apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: 'config.gradle'

def compose_version = '1.0.0-alpha02'

android {
    compileSdkVersion versions.platform.compileSdk

    defaultConfig {
        // default configured applicationId and testApplicationId are necessary only to run UI instrumented tests.
        applicationId "com.distillery.android.blueprints"
        testApplicationId "com.distillery.android.blueprints.test"

        vectorDrawables.useSupportLibrary = true

        minSdkVersion versions.platform.minSdk
        targetSdkVersion versions.platform.targetSdk

        versionCode generateVersionCode()
        versionName generateVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            // Disable PNGs crunching for debug builds
            crunchPngs false
        }
    }
    buildFeatures {
        viewBinding = true
    }
    compileOptions {
        sourceCompatibility versions.platform.java
        targetCompatibility versions.platform.java
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        animationsDisabled = true
    }
    composeOptions {
        kotlinCompilerVersion '1.4.0'
        kotlinCompilerExtensionVersion "$compose_version"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(path: ':data')
    implementation project(path: ':domain')
    implementation project(path: ':mvi')
    implementation project(path: ':mvi_example')
    implementation project(path: ':mvp')
    implementation project(path: ':mvp_example')
    implementation project(path: ':mvvm')
    implementation project(path: ':mvvm_example')

    implementation project(path: ':ui-compose')

    // Platform libraries
    implementation libraries.platform.kotlinStdLib
    implementation libraries.platform.kotlinCoroutines
    implementation libraries.platform.kotlinCoroutinesAndroid

    // Room
    implementation libraries.persistence.roomRuntime
    implementation libraries.persistence.roomKtx
    kapt libraries.persistence.roomCompiler

    // Android libraries
    implementation libraries.android.core
    implementation libraries.android.supportAppCompat
    implementation libraries.android.supportConstraintLayout

    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"

    // Unit tests
    testImplementation libraries.test.junit

    // Koin
    implementation libraries.koin.android
    implementation libraries.koin.viewModel
    testImplementation libraries.koin.test
}
repositories {
    maven { url 'https://jitpack.io' }
}