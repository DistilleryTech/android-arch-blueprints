// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: 'dependencies.gradle'

    repositories {
        mavenCentral()
        google()
        jcenter()
        
    }
    dependencies {
        classpath libraries.platform.gradle
        classpath libraries.platform.kotlinGradlePlugin

        // for detekt plugin
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.7.4"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }

    ext {
        // List of all build types permutations is needed to generate
        // fine-grained dependencies for each build version.
        // Duplicates the app.gradle b/c we can't retrieve it from the app.gradle
        // so the duplication is unavoidable
        buildTypes = ["Debug", "Release"]
    }
}

apply {
    plugin 'io.gitlab.arturbosch.detekt'
    // style check for Java code
    from 'check-style.gradle'
    // style check for Kotlin code
    from 'detekt.gradle'
    // style check for XML
    from 'lint.gradle'
    // all-in-one check runner
    from 'build_check.gradle'    
    // configures pre-push hook for Git
    from "gradle/githooks_config.gradle.kts"
}


allprojects {
    repositories {
        google()
        jcenter()
        
    }
    // maxParallelForks controls the parallelism of a single Test task.
    // So with maxParallelForks a single Test could run up to CPU/2 tests in parallel using separate test processes.
    tasks.withType(Test) {
        // half of CPUs but no fewer than 1
        maxParallelForks = Math.max(1, Runtime.runtime.availableProcessors().intdiv(2))
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
